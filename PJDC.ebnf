
# Begin
<Translation_Unit> ::= {<Function_Definition> | <Variable_Definition> | <Array_Definition>}CODE_EOF



# Definition, Declaration
<Type> ::= VOID | CHAR | INT | LONG
<Function_Definition> ::= <Type> IDENTIFIER "(" ")" <Compound_Statement>

<Variable_Definition> ::= <Variable_Declaration>["="<Expression>]";"
<Variable_Declaration> ::= <Type>["*"] IDENTIFIER

<Array_Definition> ::= <Type>IDENTIFIER"["CONSTANT_INT"]" ";"


# Statement
<Statement> ::= <Compound_Statement> | <Variable_Definition> | <If_Statement> | <Iteration_Statement> | 
              | <Return_Statement> | <Expression_Statement>

<Compound_Statement> ::= "{"{<Statement>}"}"

<If_Statement> ::= IF"("<Expression>")"<Statement>[ELSE<Statement>]

<Iteration_Statement> ::= <While_Statement> | <DoWhile_Statement>
<While_Statement> ::= WHILE"("<Expression>")"<Statement>
<DoWhile_Statement> ::= DO<Statement>WHILE"("<Expression>")"";"

<Return_Statement> ::= RETURN [<Expression>] ";"

<Expression_Statement> ::= [<Expression>]";"



# Expression
<Expression> ::= <Assignment_Expression>{","<Assignment_Expression>}
<Assignment_Expression> ::= [(["*"]IDENTIFIER) | (IDENTIFIER"["<Expression>"]") "="]<Equality_Expression>

<Equality_Expression> ::= <Relational_Expression>{("==" | "!=")<Relational_Expression>}
<Relational_Expression> ::= <PlusMinus_Expression>{("<" | "<=" | ">" | ">=")<PlusMinus_Expression>}

<PlusMinus_Expression> ::= <MulDiv_Expression>{("+" | "-")<MulDiv_Expression>}
<MulDiv_Expression> ::= <Unary_Expression>{("*" | "/")<Unary_Expression>}

<Unary_Expression> ::= <Primary_Expression> 
<Primary_Expression> ::= <FunctionCall_Expression> | <Address_Expression> | <Dreference_Expression> | <Array_Expression> |
                        | CONSTANT_INT | CONSTANT_CHAR | CONSTANT_STRING | IDENTIFIER

<FunctionCall_Expression> ::= IDENTIFIER"("<Expression>")"

<Address_Expression> ::="&"IDENTIFIER [("+ "| "-") CONSTANT_INT]
<Dreference_Expression> ::="*" ( IDENTIFIER | "(" IDENTIFIER [("+" | "-") CONSTANT_INT] ")" )
<Array_Expression> ::= IDENTIFIER"[" <Expression> "]"
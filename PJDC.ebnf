<Translation_Unit> ::= {<Statement>}CODE_EOF

<Statement> ::= <Print_Statement> | <Variable_Definition> | <Assignment_Statement> | <Compound_Statement> 
                | <If_Statement> | <Iteration_Statement>

<Compound_Statement> ::= "{"{<Statement>}"}"

<Print_Statement> ::= PRINT <Expression>";"

<Assignment_Statement> ::= IDENTIFIER"="<Expression>";"

<Variable_Definition> ::= <Variable_Declaration>["="<Expression>]";"
<Variable_Declaration> ::= INT IDENTIFIER
 
<If_Statement> ::= IF"("<Expression>")"<Statement>[ELSE<Statement>]

<Iteration_Statement> ::= <While_Statement> | <DoWhile_Statement>
<While_Statement> ::= WHILE"("<Expression>")"<Statement>
<DoWhile_Statement> ::= DO<Statement>WHILE"("<Expression>")"";"



<Expression> ::= <Equality_Expression>

<Equality_Expression> ::= <Relational_Expression>{("==" | "!=")<Relational_Expression>}
<Relational_Expression> ::= <PlusMinus_Expression>{("<" | "<=" | ">" | ">=")<PlusMinus_Expression>}

<PlusMinus_Expression> ::= <MulDiv_Expression>{("+" | "-")<MulDiv_Expression>}
<MulDiv_Expression> ::= <Unary_Expression>{("*" | "/")<Unary_Expression>}
<Unary_Expression> ::= <Primary_Expression> | (("+" | "-")<Unary_Expression>)
<Primary_Expression> ::= CONSTANT_INT | IDENTIFIER